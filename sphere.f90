!  GLUT FORTRAN 90 PROGRAM TO DRAW RED LIGHT SPHERE.
MODULE SPHERE
  IMPLICIT NONE

CONTAINS

  SUBROUTINE DISPLAY() BIND(C)
    USE OPENGL_GL ,ONLY: GLCLEAR, GLCALLLIST, GL_COLOR_BUFFER_BIT, GL_DEPTH_BUFFER_BIT
    USE OPENGL_GLUT ,ONLY: GLUTSWAPBUFFERS
    IMPLICIT NONE
    CALL GLCLEAR(GL_COLOR_BUFFER_BIT + GL_DEPTH_BUFFER_BIT)
    CALL GLCALLLIST(1)
    CALL GLUTSWAPBUFFERS
  END SUBROUTINE DISPLAY

  SUBROUTINE GFXINIT
    USE OPENGL_GL
    USE OPENGL_GLU ,ONLY: GLUPERSPECTIVE, GLULOOKAT
    USE OPENGL_GLUT ,ONLY: GLUTSOLIDSPHERE
    IMPLICIT NONE
    REAL(GLFLOAT) :: DIFFUSE(4) = [1.0, 0.0, 0.0, 1.0], POS(4) = [1.0, 1.0, 1.0, 0.0]
    CALL GLNEWLIST(1, GL_COMPILE)
    CALL GLUTSOLIDSPHERE(1.0_GLDOUBLE, 40, 40)
    CALL GLENDLIST
    CALL GLLIGHTFV(GL_LIGHT0, GL_DIFFUSE, DIFFUSE)
    CALL GLLIGHTFV(GL_LIGHT0, GL_POSITION, POS)
    CALL GLENABLE(GL_LIGHTING)
    CALL GLENABLE(GL_LIGHT0)
    CALL GLENABLE(GL_DEPTH_TEST)
    CALL GLMATRIXMODE(GL_PROJECTION)
    CALL GLUPERSPECTIVE(40.0_GLDOUBLE, 1.0_GLDOUBLE, &
      1.0_GLDOUBLE, 10.0_GLDOUBLE)
    CALL GLMATRIXMODE(GL_MODELVIEW)
    CALL GLULOOKAT( &
      0.0_GLDOUBLE, 0.0_GLDOUBLE, 5.0_GLDOUBLE, &
      0.0_GLDOUBLE, 0.0_GLDOUBLE, 0.0_GLDOUBLE, &
      0.0_GLDOUBLE, 1.0_GLDOUBLE, 1.0_GLDOUBLE)
    CALL GLTRANSLATEF(0.0, 0.0, -1.0)
  END SUBROUTINE GFXINIT

END MODULE SPHERE

PROGRAM MAIN
  USE OPENGL_GLUT
  USE SPHERE
  IMPLICIT NONE

  INTEGER :: I

  CALL GLUTINIT()
  CALL GLUTINITDISPLAYMODE(GLUT_SINGLE+GLUT_RGB+GLUT_DEPTH)
  I = GLUTCREATEWINDOW('FORTRAN GLUT SPHERE')
  CALL GFXINIT
  CALL GLUTDISPLAYFUNC(DISPLAY)
  CALL GLUTMAINLOOP
END PROGRAM MAIN
